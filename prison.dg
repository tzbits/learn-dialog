(story title)   Prison Escape
(story author)  tzbits
(story noun)    a dynamic map game
(story ifid)    5A8DB36C-417B-4FF8-9EDD-6C5A70DC5E67

(style class @status)   height: 1em;
(style class @italic)   font-style: italic;

(intro)
	(try [look])

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

(global variable (depth 0))

(before [go #down])
	(depth $N) ($N plus 1 into $Np1)
	(now) (depth $Np1)

(before [go #up])
	(depth $N) ($N minus 1 into $Nm1)
	(now) (depth $Nm1)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

#spoon
(item *)
(name *)        spoon
(* is #in #prison-cell)
(* is handled)

(prevent [dig $])
	~(#spoon is #heldby #player)
	If only you could find something to dig with.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

#prison-cell
(room *)
(dig-node *)
(starting-dig-node *)
(name *)        prison cell
(heads *)       cell
(look *)        The prison cell has four walls and seemingly no way out.

(prevent [leave * $Dir])
	~($Dir = #down)
	It's no use. You are locked in here.

(prevent [dig $Dir])
	(current room *)
	(#spoon is #heldby #player)
	~($Dir = #down)
	You can't dig (name $Dir) from here.

(prevent [dig $Dir])
	(current room *)
	(#spoon is #heldby #player)
	~($Dir = #down)
	You can't dig that way from here.

(narrate failing to leave * #down)
	~(#spoon is #heldby #player)
	If only you could find something to dig with.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

(flip [#north #south])
(flip [#south #north])
(flip [#east #west])
(flip [#west #east])
(flip [#up #down])
(flip [#down #up])

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

(global variable (tunnel-list []))

(tunnel $Room $Dir $Dest)
	(tunnel-list $List)
	(tunnel-search $Room $Dir $Dest $List)

(tunnel-search $Room $Dir $Dest $List)
	([$Room $Dir $Dest | $Tail] = $List)
	(or) { ([$ $ $ | $Tail] = $List) (tunnel-search $Room $Dir $Dest $Tail) }

(excavated $Room)
	(tunnel $Room $ $) (or) (tunnel $ $ $Room)

(fully-excavated $Room)
	(starting-dig-node $Room)
	(tunnel $Room $ $)

(fully-excavated $Room)
	(tunnel $Room $ $)
	(tunnel $ $ $Room)

(from $A go $Dir to $B)
	(dig-node $A)
	(tunnel $A $Dir $B)
	(or) { (flip [$Dir $Back]) (tunnel $B $Back $A) }

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

(generate 6 (dig-node $))

(room *(dig-node $))
(name $Room)
	(dig-node $Room)
	tunnel

(look $Room)
	(dig-node $Room)
	You are in a small spoon-dug tunnel.
	(if) (tunnel $Room $Dir1 $) (then)
		The tunnel continues (name $Dir1).
	(endif)
	(if) (tunnel $ $Dir2 $Room) (then)
		(flip [$Dir2 $Back])
		(Name $Back) leads back.
	(endif)

(interface (unused-dig-nodes $>List))

(unused-dig-nodes $List)
	(collect $Node)
		*(dig-node $Node) ~(starting-dig-node $Node) ~(excavated $Node)
	(into $List)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

(grammar [dig [direction]] for [dig $])
(grammar [dig] for itself)

(perform [dig])
	In which direction do you want to dig?
	(asking for direction in [dig []])

(prevent [dig $])
	(current room $Room)
	(fully-excavated $Room)
	You already dug here.

(perform [dig $Dir])
	(unused-dig-nodes [])
	You try digging (name $Dir), but hit a wall of rock that prevents further
	progress.

(perform [dig $Dir])
	(current room $Room)
	(unused-dig-nodes $Unused)
	([$Dest | $] = $Unused)
	(tunnel-list $TunnelList)
	(now) (tunnel-list [$Room $Dir $Dest | $TunnelList])
	Determined to escape, you begin digging with the spoon, and slowly extend
	the tunnel (name $Dir).

(instead of [go #up])
	(current room $Room)
	(from $Room go #up to $Dest)
	~(starting-dig-node $Dest)
	(depth $depth)
	($depth = 0)
	Your spoon breaks through to daylight. You poke your head out of the
	tunnel.
	(par) There is a prison guard here.
	(par) He takes you back to prison.
	(game over { Game Over. })

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

#player
(current player *)
(#player is #in #prison-cell)
